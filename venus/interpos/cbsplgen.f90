!
!.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.     
!
      SUBROUTINE CBSPLGEN(PXIN,PYIN,PYINNEW,PYINPP,KNIN,PXOUT,PYOUT, &
     &  PYOUTP,PYOUTPP,KNOUT,KOPT,PSIG,KWORK,PWORK,PAMAT,MDMATOT,NBCLFT &
     &  ,NBCRGT,XBCLFT,XBCRGT,YBCLFT,YBCRGT,KEXTRPOL,PXEXP0,PXEXPDL &
     &  ,KFLAG)
!     =================================================================
!
!     NOTE: THIS ROUTINE INCLUDES THE STANDARD CUBIC SPLINE IF PTAUS=0 (i.e. psig=0):
!           THEN PYINNEW IS NOT USED AND MDAMAT=3 IS SUFFICIENT
!           (=> PYINNEW(1) OR PYINNEW=PYIN IS OK)
!
!     Interpolate (pxin,pyin) on (pxout,pyout) using
!     Hirshman fitted cubic spline with ptaus value or
!     standard cubic spline if PTAUS=0 (psig=ptaus*sigma/sigmamin)
!
!     KOPT = 0: ONLY INTERPOLATE FUNCTION INTO PYOUT
!     KOPT = 1: INTERPOLATE FUNCTION INTO PYOUT AND 1ST DER. INTO PYOUTP
!     KOPT = 2: AS KOPT=1 PLUS 2ND DER. INTO PYOUTPP
!
!     MDMATOT = TOTAL DIMENSION OF PAMAT. THE REQUIRED SPACE DEPENDS IF
!     .         PTAUS IS ZERO OR NOT AND ON THE B.C. (SYMMETRIC OR NOT)
!     THUS, MDMATOT CAN VARY BETWEEN 2*KNIN AND 10*KNIN (MAX. VALUE NEEDED)
!
!     SEE COMMENTS FOR ROUTINE CBFITBND FOR MORE INFORMATION
!
!     IF LAPACK ROUTINES NOT AVAILABLE, USE spgbtrf_s.f
!     (LAPACK SOURCE COPIED FROM NETLIB.ORG)
!
!     PXIN    : INPUT ABSCISSA (GIVEN DATA)
!     PYIN    : INPUT VALUES OF FUNCTION AT PXIN(I),I=1,KNIN
!     PYINNEW : IF PTAUS.NE.0, THEN PYNEW CONTAINS ON OUTPUT THE NEW VALUES
!     .         OF THE FUNCTION AT PXIN(I) FOR THE CUBIC SPLINE FIT
!     PYINPP  : SECOND DER. OF THE CUBIC SPLINE FIT FOR (PXIN,PYIN) IF PTAUS=0 OR
!     .         ON (PXIN,PYNEW) OTHERWISE
!     KNIN    : NUMBER OF INPUT POINTS
!     PXOUT   : X VALUES AT WHICH THE FUNCTION HAS TO BE INTERPOLATED (INPUT)
!     PYOUT   : INTERPOLATED VALUES AT PXOUT(I),I=1,KNOUT (OUTPUT)
!     PYOUTP  : INTERPOLATED VALUES OF 1ST DER. OF FUNCTIONS AT PXOUT(I) (OUTPUT, IF KOPT.GE.1)
!     PYOUTPP : INTERPOLATED VALUES OF 2ND DER. OF FUNCTIONS AT PXOUT(I) (OUTPUT, IF KOPT.EQ.2)
!     KNOUT   : NUMBER OF POINTS FOR OUTPUT
!     KOPT    : SEE ABOVE
!     PSIG    : SIGMA at each point, normalized by minimum sigma, times PTAUS
!     PTAUS   : WEIGHT OF SECOND DERIVATIVE IN THE CHI**2 TO BE MINIMIZED. PTAUS=0 GIVES THE
!     .         STANDARD CUBIC SPLINE. LARGER VALUES OF PTAUS WILL SMOOTH MORE THE 2ND DER.
!     KWORK   : INTEGER WORK SPACE OF DIMENSION 5*KNIN
!     PWORK   : REAL WORK SPACE OF DIMENSION 2*KNIN
!     PAMAT   : REAL WORK SPACE FOR THE MATRIX OF DIMENSION MDMATOT
!     MDMATOT : DIMENSION OF PAMAT, WHICH VALUE SHOULD BE IN [2*KNIN,10*KNIN], DEPENDING ON
!     .         PTAUS BEING 0 OR NOT AND ON THE BOUNDARY CONDITION (SEE PARAGRAPH 1. BELOW)
!     NBCLFT  : FOR LEFT B.C., VALUE SHOULD BE 0,1,2,10,11 OR 12. (SEE ROUTINE CBFITBND BELOW)
!     NBCRGT  : FOR RIGHT B.C. (SEE ROUTINE CBFITBND BELOW)
!     XBCLFT  : FOR LEFT B.C., USED ONLY IF NBCLFT.GE.10 (SEE ROUTINE CBFITBND BELOW)
!     XBCRGT  : FOR RIGHT B.C., USED ONLY IF NBCRGT.GE.10 (SEE ROUTINE CBFITBND BELOW)
!     YBCLFT  : VALUE OF LEFT B.C.
!     YBCRGT  : VALUE OF RIGHT B.C.
!     .         STANDARD B.C. (SECOND DER. = 0) IS OBTAINED WITH:
!     .         NBCLFT = NBCRGT = 0 AND YBCLFT = YBCRGT = 0.
!     KEXTRPOL: OPTION ON HOW TO EXTRAPOLATE THE FUNCTION IF PXOUT(I) IS OUTSIDE [PXIN(1),PXIN(KNIN)]
!     .       = 0: STOP WITH ERROR MESSAGE IF OUT OF BOUND
!     .       = 1: LINEAR EXTRAPOLATION
!     .       = 2: USE QUADRATIC INTERPOLATION IF X OUT OF BOUND
!     .       = 3: USE CUBIC INTERPOLATION IF X OUT OF BOUND
!     .       = 21: USE QUADRATIC WITHIN ALFA*DELTA_X AND LINEAR FURTHER
!     .       = 31: USE CUBIC WITHIN ALFA*DELTA_X AND LINEAR    FURTHER
!     .       = 32: USE CUBIC WITHIN ALFA*DELTA_X AND QUADRATIC FURTHER
!     PXEXP0  : PTAUS IS WEIGHTED BY AN EXP(-((X-PXEXP0)/PXEXPDL)**2)
!     PXEXPDL : IF PXEXP0 NOT IN [PXIN(1),PXIN(KNIN)], EXP() IGNORED AND PTAUS=CST
!     .         (SEE ROUTINE CBFITBND BELOW)
!     .         GIVE PXEXP0=PXIN(1)-1._RKIND AND PXEXPDL=1. TO GET CONSTANT PTAUS
!     KFLAG   : ERROR FLAG: IF NOT 0, THERE IS A PROBLEM
!
!-----------------------------------------------------------------------
!c      implicit REAL(RKIND) :: (p)
      USE prec_rkind
      implicit none
      REAL(RKIND) :: EPSILON
      PARAMETER(EPSILON = 1.0E-10_RKIND)
!
      integer knin, knout, kopt, mdmatot, nbclft, nbcrgt, kextrpol, &
     &  kflag, i, idamat
      REAL(RKIND) :: pxin, pyin, pyinnew, pyinpp, pxout, pyout, pyoutp, pyoutpp &
     &  ,psig, pamat, pwork
      integer kwork
      DIMENSION :: PXIN(KNIN), PYIN(KNIN), PYINNEW(KNIN), PYINPP(KNIN), &
     &  PXOUT(KNOUT), PYOUT(KNOUT), PYOUTP(KNOUT), PYOUTPP(KNOUT), &
     &  PSIG(KNIN)
!   
      DIMENSION :: PAMAT(MDMATOT), PWORK(2*KNIN)
      DIMENSION :: KWORK(5*KNIN)
!
      REAL(RKIND) :: xbclft, xbcrgt, ybclft, ybcrgt, pxexp0, pxexpdl, zy, zyp, &
     &  zypp
!
!-----------------------------------------------------------------------
!     0. CHECK INPUT CONSISTENCY
!
      KFLAG = 0
      IF (PSIG(1) .EQ. 0._RKIND) THEN
        IF (NBCLFT.GE.10 .OR. NBCRGT.GE.10 .OR. NBCLFT.EQ.2 &
     &  .OR. NBCRGT.EQ.2) THEN
          PRINT *,' PTAUS=0, BUT NEED SMOOTHING, WHEN'
          PRINT *,'     NBCLFT.GE.10 .OR. NBCRGT.GE.10 .OR.', &
     &      ' NBCLFT.EQ.2 .OR. NBCRGT.EQ.2'
          PRINT *,' NBCLFT = ',NBCLFT
          PRINT *,' NBCRGT = ',NBCRGT
!%OS          STOP 'TAU=0'
          KFLAG = 1
          return
        ENDIF
      ENDIF
!
!   PXIN in ASCENDING ORDER
!
      DO i=1,KNIN-1
        if (PXIN(i) .GE. PXIN(i+1)) then
          print *,' xin not in ascending order:'
          print *,' xin(',i,')= ',PXIN(i),'   >=   xin(',i+1,')= ', &
     &      PXIN(i+1)
          KFLAG = 2
          RETURN
        endif
      END DO
!
!     1. PREPARE MATRIX DIMENSION.
!     MINIMUM REQUIRED:
!     IUP = 1 = IDOWN; IF PTAUS.NE.0 => IUP = IDOWN = 2
!     IF SYMMETRIC, USE ONLY UPPER BAND AND DIAGONAL =>IDAMAT=IUP+1
!     IF ASYMMETRIC => IDAMAT = 2*IDOWN + IUP + 1
!     IF B.C. NOT AT END OF INTERVAL => IUP = IUP + 1, AND/OR IDOWN=IDOWN+1
!
!     => ALTOGETHER, MINIMUM VALUE: IDOWN=1, IUP=1, SYM. =>IDAMAT_MAX = 2
!     => ALTOGETHER, MAXIMUM VALUE: IDOWN=3, IUP=3 =>IDAMAT_MAX = 10
!
      IDAMAT = 3
      IF (PSIG(1) .EQ. 0._RKIND) IDAMAT = 2
      IF (NBCLFT.GE.10 .OR. NBCRGT.GE.10 .OR. NBCLFT.EQ.2 &
     &  .OR. NBCRGT.EQ.2) IDAMAT = 3*(IDAMAT-1)+1
      IF (NBCLFT .GE. 10) IDAMAT = IDAMAT + 1
      IF (NBCRGT .GE. 10) IDAMAT = IDAMAT + 2
!
      IF (MDMATOT .LT. IDAMAT*KNIN) THEN
        PRINT *,' '
        PRINT *,' DIMENSION MDMATOT= ',MDMATOT,' IS TOO SMALL,', &
     &    ' NEED IDAMAT*KNIN= ',IDAMAT,'*',KNIN,' = ',IDAMAT*KNIN
!%OS        STOP
        RETURN
      ENDIF
!
      CALL CBFITBND(PXIN,PYIN,PYINNEW,KNIN,PYINPP,PSIG,KWORK, &
     &  PWORK(1),PWORK(KNIN+1),PAMAT,IDAMAT,NBCLFT,NBCRGT, &
     &  XBCLFT,XBCRGT,YBCLFT,YBCRGT,PXEXP0,PXEXPDL)
!
!L    2. COMPUTE INTERPOLATED VALUE AT EACH PXOUT
!
      DO I=1,KNOUT
        IF (PSIG(1) .EQ. 0.0_RKIND) THEN
          CALL SPLIBND(PXIN,PYIN   ,PYINPP,KNIN,PXOUT(I),ZY,ZYP,ZYPP, &
     &      KEXTRPOL)
        ELSE
          CALL SPLIBND(PXIN,PYINNEW,PYINPP,KNIN,PXOUT(I),ZY,ZYP,ZYPP, &
     &      KEXTRPOL)
        ENDIF
        PYOUT(I) = ZY
        IF (KOPT .GE. 1) PYOUTP(I) = ZYP
        IF (KOPT .EQ. 2) PYOUTPP(I) = ZYPP
      END DO
!
      RETURN
      END SUBROUTINE CBSPLGEN
